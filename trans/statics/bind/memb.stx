module statics/bind/memb

imports

    statics/common

rules

// FIXME: seems to cause the Statix solver to get in an infinite loop
//    declareMembs : scope * list((ID * TYPE))
//
//    declareMembs(s, [(name, T) | xs]) :-
//        declareMemb(s, name, T),
//        declareMembs(s, xs).

    declareMemb : scope * ID * TYPE

    declareMemb(s, name, T) :-
        // TODO: add information about mutability and other access modifiers of the member
        !memb[name, T] in s,
        queryMemb(s, name) == [(_, (_, _))] | error $[duplicate definition]@name.

rules

    queryMembLocal : scope * ID -> list((path * (ID * TYPE)))

    queryMembLocal(s, name) = p :-
        query memb
            filter P* and { x :- x == name }
            min $ < P and false
            in s |-> p.

rules

    queryMemb : scope * ID -> list((path * (ID * TYPE)))

    queryMemb(s, name) = p :-
        query memb
            filter P* I? M? and { x :- x == name }
            min $ < P, $ < M and false
            in s |-> p.

rules

    resolveMemb : scope * ID -> TYPE

    resolveMemb(s, name) = T :- {p name'}
        queryMemb(s, name) == p,
        p == [(_, (name', T))|_],
        @name.ref := name'.

rules

    resolveMembLocal : scope * ID -> TYPE

    resolveMembLocal(s, name) = T :- {p p'}
        queryMembLocal(s, name) == p,
        p == [(p', (_, T))],
        @name.type := T.
