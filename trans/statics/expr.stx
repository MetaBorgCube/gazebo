module statics/expr

imports

    statics/common
    statics/expr/nbt
    statics/expr/selector
    statics/expr/tag-resource
    statics/expr/literal
    statics/expr/ref
    statics/expr/invoke
    statics/expr/arith
    statics/expr/range
    statics/expr/vec
    statics/expr/construct
    statics/types

rules

    typeOfExpr : scope * Expr -> TYPE

    typeOfExpr(s, e) = T@typeOfExprImpl(s, e) /*:- @e.type := T*/.

    /** do not refer directly, use typeOfExpr */
    typeOfExprImpl : scope * Expr -> TYPE

rules // NBT

    typeOfExprImpl(s, NBT2Expr(nbt)) = T :-
        typeOfNBT(s, nbt) == T.

rules // selector

    typeOfExprImpl(s, Selector2Expr(sel)) = T :-
        typeOfSelector(s, sel) == T.

rules // tag & resource

    typeOfExprImpl(s, Resource2Expr(res)) = T :-
        typeOfResource(s, res) == T.

    typeOfExprImpl(s, Tag2Expr(tag)) = T :-
        typeOfTag(s, tag) == T.

rules // literal

    typeOfExprImpl(s, Literal2Expr(lit)) = T :-
        typeOfLiteral(s, lit) == T.

rules // ref

    typeOfExprImpl(s, Ref2Expr(ref)) = T :-
        typeOfRef(s, ref) == T.

rules // find

    typeOfExprImpl(s, Find2Expr(find)) = T :-
        try { false } | note $[todo].

rules // invoke

    typeOfExprImpl(s, Invoke2Expr(i)) = T :-
        typeOfInvoke(s, i) == T.

rules // arithmetic

    typeOfExprImpl(s, Arith2Expr(arith)) = T :-
        typeOfArith(s, arith) == T.

rules // compare (boolean logic)

    typeOfExprImpl(s, And(Expr2Expr-I(l), Expr2Expr-I(r))) = BOOL() :- {Tl Tr}
        typeOfExpr(s, l) == Tl,
        typeOfExpr(s, r) == Tr,
        typeBoolish(Tl) | error $[cannot and non-boolean value]@l,
        typeBoolish(Tr) | error $[cannot and non-boolean value]@r.

    typeOfExprImpl(s, Or(Expr2Expr-I(l), Expr2Expr-I(r))) = BOOL() :- {Tl Tr}
        typeOfExpr(s, l) == Tl,
        typeOfExpr(s, r) == Tr,
        typeBoolish(Tl) | error $[cannot or non-boolean value]@l,
        typeBoolish(Tr) | error $[cannot or non-boolean value]@r.

    typeOfExprImpl(s, Not(Expr2Expr-I(e))) = BOOL() :- {Te}
        typeOfExpr(s, e) == Te,
        typeBoolish(Te) | error $[cannot negate non-boolean value]@e.

    typeOfExprImpl(s, Cmp(l, op, r)) = BOOL() :- {Tl Tr}
        try { false } | note $[todo].

rules // range

    typeOfExprImpl(s, Range2Expr(range)) = T :-
        T == typeOfRange(s, range).

rules // vector (position)

    typeOfExprImpl(s, Vec2Expr(vec)) = T :-
        T == typeOfVec(s, vec).

rules // lambda

    typeOfExprImpl(s, Lambda2Expr(lambda)) = LAMBDA() :-
        try { false } | note $[future].

rules // construct

    typeOfExprImpl(s, Construct2Expr(constr)) = T :-
        T == typeOfConstruct(s, constr).
