module statics/bind/registration

imports

    statics/common

rules

    /** parent registration scope * registration name * this registration scope */
    declareRegistration : scope * NSIDi * scope

    declareRegistration(s_registry, name, s_registration) :-
        !registration[name, s_registration] in s_registry,
        queryRegistration(s_registry, name) == [(_, (_, _))] | error $[duplicate definition]@name.

rules

    queryRegistration : scope * NSIDi -> list((path * (NSIDi * scope)))

    queryRegistration(s, name) = p :-
        query registration
            filter e and { x :- x == name }
            min and false
            in s |-> p.

rules

    queryRegistrationNamePart : scope * list(string) -> list((path * (NSIDi * scope)))

    queryRegistrationNamePart(s, name) = p :-
        query registration
            filter e and { x :- x == NSIDiFull(_, name) }
            min and false
            in s |-> p.

rules

    resolveRegistration : scope * NSIDi -> (NSIDi * scope)

    resolveRegistration(s_registry, name@NSIDiShort(_)) = res :- {curr abs}
        currentMod(s_registry) == (curr, _),
        abs == absolute(name, curr),
        res == resolveRegistration(s_registry, abs).

    resolveRegistration(s_registry, name@NSIDiFull(_, _)) = (id_registration, s_registration) :- {p name'}
        queryRegistration(s_registry, name) == p,
        resolveRegistration_firstOrAmb(name, p) == (id_registration, s_registration).

    resolveRegistration(s_registry, name@NSIDiRel(rel)) = (id_registration, s_registration) :- {all}
        queryRegistrationNamePart(s_registry, rel) == all,
        resolveRegistration_firstOrAmb(name, all) == (id_registration, s_registration).

    /* source name (for ambiguity error) * query res */
    resolveRegistration_firstOrAmb : NSIDi * list((path * (NSIDi * scope))) -> (NSIDi * scope)
    resolveRegistration_firstOrAmb(_, []) = _ :- false.
    resolveRegistration_firstOrAmb(src, [(_, (name, s)) | tail]) = (name, s) :-
        tail == [] | error $[ambiguous]@src.

rules

    /**
     * current general scope * all registries * registration name (possibly relative) -> (registry, registration)
     *
     * Resolve both the registry and corresponding registration
     * from just a single registration name.
     * Will error if there is any ambiguity between possible registries
     * or registrations within registries.
     */
    resolveRegistryAndRegistrationFromAllRegistries : scope * list((NSIDi * scope)) * NSIDi -> ((NSIDi * scope) * (NSIDi * scope))

    // special overload for short NSIDi, because it is just syntactic sugar,
    // this will be delegated to the next implementation of the rule
    resolveRegistryAndRegistrationFromAllRegistries(s, all, name@NSIDiShort(_)) = res :- {curr abs}
        currentMod(s) == (curr, _),
        abs == absolute(name, curr),
        res == resolveRegistryAndRegistrationFromAllRegistries(s, all, abs).

    resolveRegistryAndRegistrationFromAllRegistries(_, all, name) = res :- {qres}
        qres == intern_queryAll(all, name),
        res == resolveRegistryAndRegistrationFromAllRegistries_firstOrAmb(name, qres).

    resolveRegistryAndRegistrationFromAllRegistries_firstOrAmb : NSIDi * list(((NSIDi * scope) * (NSIDi * scope))) -> ((NSIDi * scope) * (NSIDi * scope))
    resolveRegistryAndRegistrationFromAllRegistries_firstOrAmb(_, []) = _ :- false.
    resolveRegistryAndRegistrationFromAllRegistries_firstOrAmb(src, [x|xs]) = x :-
        xs == [] | error $[ambiguous]@src.

    intern_stripPath : list((path * (NSIDi * scope))) -> list((NSIDi * scope))
    intern_stripPath([]) = [].
    intern_stripPath([(_, (n, s)) | xs]) = [(n, s) | intern_stripPath(xs)].

    intern_queryAll : list((NSIDi * scope)) * NSIDi -> list(((NSIDi * scope) * (NSIDi * scope)))
    intern_queryAll([], _) = [].
    intern_queryAll(all, name@NSIDiFull(_, _)) = intern_queryAll_abs(all, name).
    intern_queryAll(all, name@NSIDiRel(rel)) = intern_queryAll_rel(all, rel).

    intern_queryAll_abs : list((NSIDi * scope)) * NSIDi -> list(((NSIDi * scope) * (NSIDi * scope)))
    intern_queryAll_abs([], _) = [].
    intern_queryAll_abs([reg | xs], name) = conc :- {s_registry qres qres_paired}
        reg == (_, s_registry),
        qres == intern_stripPath(queryRegistration(s_registry, name)),
        qres_paired == pairL(reg, qres),
        conc == concatQueryAllRes(qres_paired, intern_queryAll_abs(xs, name)).

    intern_queryAll_rel : list((NSIDi * scope)) * list(string) -> list(((NSIDi * scope) * (NSIDi * scope)))
    intern_queryAll_rel([], _) = [].
    intern_queryAll_rel([reg | xs], name) = conc :- {s_registry qres qres_paired}
        reg == (_, s_registry),
        qres == intern_stripPath(queryRegistrationNamePart(s_registry, name)),
        qres_paired == pairL(reg, qres),
        conc == concatQueryAllRes(qres_paired, intern_queryAll_rel(xs, name)).

    /* left-pair all elements from the right list with a constant element */
    pairL : (NSIDi * scope) * list((NSIDi * scope)) -> list(((NSIDi * scope) * (NSIDi * scope)))
    pairL(_, []) = [].
    pairL(l, [x|xs]) = [(l, x) | pairL(l, xs)].

    concatQueryAllRes : list(((NSIDi * scope) * (NSIDi * scope))) * list(((NSIDi * scope) * (NSIDi * scope))) -> list(((NSIDi * scope) * (NSIDi * scope)))
    concatQueryAllRes([], r) = r.
    concatQueryAllRes([x|xs], r) = [x | concatQueryAllRes(xs, r)].
