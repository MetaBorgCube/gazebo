module statics/tli/registry

imports

    statics/common
    statics/bind/registry
    statics/bind/registration
    statics/bind/regmeta
    statics/bind/bs
    statics/expr/literal
    statics/types
    statics/type/bounds

rules

    /** root scope * module scope * registry */
    registryOk : scope * scope * Registry

    registryOk(s_root, s_mod, DynamicRegistry(RegistryName(nsid))) :- {s_reg}
        nsid == NSID(_, _) | error $[dynamic registry name must include a namespace]@nsid,
        new s_reg, s_reg -P-> s_mod,
        declareRegistry(s_root, nsid, s_reg).

    registryOk(s_root, s_mod, StaticRegistry(RegistryName(nsid), RegistryMeta(meta))) :- {s_reg}
        nsid == NSID(_, _) | error $[static registry name must include a namespace]@nsid,
        new s_reg, s_reg -P-> s_mod,
        registryMetasOk(s_reg, meta),
        declareRegistry(s_root, nsid, s_reg).

rules

    registryMetasOk maps registryMetaOk(*, list(*))
    /** registry scope * meta */
    registryMetaOk : scope * RegistryMetaEntry

    registryMetaOk(s, RmeProtocolId(id)) :- {s_meta}
        new s_meta,
        // TODO: declare actual value type as INT() in regmeta (scope?)
        declareRegmeta(s, "protocol_id", s_meta).

    registryMetaOk(s, RmeBlockStates(bss)) :- {s_bss}
        new s_bss,
        blockStatesOk(s, s_bss, bss),
        declareRegmeta(s, "block_states", s_bss).

    registryMetaOk(s, RmeData(t)) :- {s_data}
        COMPOUND(s_data) == typeOfType(s, t) | error $[data type must be an interface]@t,
        declareRegmeta(s, "data", s_data).

    registryMetaOk(s, RmeRegistration(id, RegistryMeta(meta))) :- {s_reg}
        id == NSID(_, _) | error $[registration name must include a namespace]@id,
        new s_reg, s_reg -P-> s,
        declareRegistration(s, id, s_reg),
        registryMetasNoRegistration(meta),
        registryMetasOk(s_reg, meta).

rules

    /** ensure that a list of registry meta entries does not contain a registration */
    registryMetasNoRegistration maps registryMetaNoRegistration(list(*))
    registryMetaNoRegistration : RegistryMetaEntry
    registryMetaNoRegistration(reg@RmeRegistration(_, _)) :-
        false | error $[registration cannot contain another registration in its metadata]@reg.
    registryMetaNoRegistration(_).

rules

    blockStatesOk maps blockStateOk(*, *, list(*))
    blockStateOk : scope * scope * RmeBlockState
    blockStateOk(s, s_bss, RmeBlockState(name, t, options)) :- {T}
        T == typeOfType(s, t),
        blockStateOptionsOk(s, T, options),
        declareBlockState(s_bss, name, T).

    blockStateOptionsOk maps blockStateOptionOk(*, *, list(*))
    blockStateOptionOk : scope * TYPE * Literal
    blockStateOptionOk(s, Tbs, lit) :- {Tlit}
        Tlit == typeOfLiteral(s, lit),
        subtype(Tlit, Tbs) | error $[type of option ([Tlit]) is not assignable to the type of block state ([Tbs])]@lit.
