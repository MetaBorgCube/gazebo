module Type

imports

    Common
    NSID
    Expr

context-free sorts

    Type
    EnumTypedMemb
    InterfaceMember

context-free syntax

    Type.TypeString = <String>
    Type.TypeBool = <Bool>
    Type.TypeInt = <Int>
    Type.TypeFloat = <Float>
    Type.TypeRef = <<NSID>>
    Type.TypeResourceRegistry = [$<[NSID]>]
    Type.TypeTag = [#]

    // TODO: think more about this notation
    Type.TypeArrayDynamic = <[<Type>;]>
    Type.TypeArrayFixedAnonymous = <[<Type>; <INT>]>
    Type.TypeArrayFixedNamed = <[<Type>; <{ID ", "}+>]>
    Type.TypeListDynamic = <[<Type>]>
    Type.TypeListFixedAnonymous = <[<Type>: <INT>]>
    Type.TypeListFixedNamed = <[<Type>: <{ID ", "}+>]>
    Type.TypeEnumUntyped = <
        enum
        {
            <{ID ",\n"}*>
        }
    >
    Type.TypeEnumTyped = <
        enum <Type>
        {
            <{EnumTypedMemb ",\n"}*>
        }
    >
    Type.TypeInterface = <
        interface
        {
            <{InterfaceMember "\n"}*>
        }
    >
    Type.TypeInterfaceCompose = <
        interface : <{Type ", "}+>
        {
            <{InterfaceMember "\n"}*>
        }
    >

    EnumTypedMemb.EnumTypedMemb = <<ID> = <Expr>>

    InterfaceMember.InterfaceMember = <<ID> <Type>>
    InterfaceMember.InterfaceMemberString = <<STRING> <Type>>

lexical syntax

    ID = "String" {reject}
    ID = "Bool" {reject}
    ID = "Int" {reject}
    ID = "Float" {reject}
    ID = "enum" {reject}

context-free sorts

    TypeDecl

context-free syntax

    TypeDecl.TypeDecl = <type <ID> <Type>>
