module common

imports

  signatures/gazebo-core/-
  signatures/gazebo/-
  // BUG: this would have been sufficient (below), but transitive imports are not so great for source deps apparently
  signatures/-

  libspoofax/-

  str_common

strategies

  pp-gzbc = call-in-language(|"gazebo-core", "pp-gazebo-core-string")

  pp-llmc = call-in-language(|"llmc", "pp-llmc-string")

strategies

  relevant-tli =
    ?TypeofGlobal-C(_, _)
  + ?Function-C(_, _, _, _)
  + ?Decl-C(_, _, _, _)

  is-ref =
    ?RefFull-C(_)
  + ?RefLocal-C(_)

  is-lit =
    ?String-C(_)
  + ?BoolT-C()
  + ?BoolF-C()
  + ?NumB-C(_)
  + ?NumS-C(_)
  + ?NumI-C(_)
  + ?NumL-C(_)
  + ?NumF-C(_)
  + ?NumD-C(_)

  is-resource-lit =
    ?ResInst-C(ResourceBare-C(_, _))
  + ?ResInst-C(ResourcePropsPayl-C(_, _, Props-C([]), NbtCompound-C([])))

  is-selector-compatible =
    is-ref
  + is-lit
  + is-resource-lit

rules // nsid-ensure-valid :: string -> string

  nsid-ensure-valid =
    explode-string
  ; map(try(
      \<is-upper> -> ['-', <to-lower>]\
    + \'$' -> ['-', '-']\
    ))
  ; flatten-list
  ; implode-string

rules // nsid-from-identf :: IdentFull-C -> NSID-LL

  nsid-from-identf: IdentFull-C(ns, name) -> NSID-LL(ns, <map(nsid-ensure-valid)> name)

rules // nsid-from-base(|[ns | basename]) :: a | List(a) -> NSID-LL

  nsid-from-base(|[ns | basename]): name -> NSID-LL(ns, <concat; map(nsid-ensure-valid)> [basename, name])
    where is-list
  nsid-from-base(|base): name -> <nsid-from-base(|base)> [name]
    where not(is-list)

rules // identf-from-base(|[ns | basename]) :: a | List(a) -> IdentFull-C

  identf-from-base(|[ns | basename]): name -> IdentFull-C(ns, <concat> [basename, name])
    where is-list
  identf-from-base(|base): name -> <identf-from-base(|base)> [name]
    where not(is-list)
