module asm

imports

  common

/**
 * NOTE:
 * Being at "top level" means not being inside an argument, for all practical purposes.
 */

strategies

  asm(|function_base) =
    {| BaseNSID, EmitFunction
    : with(rules(BaseNSID: _ -> function_base))
    ; asm-unit(true|"")
    ; <bagof-EmitFunction; reverse <+ ![]>()
    |}

strategies // asm-unit(is-toplevel) :: Unit-ASM -> string(nsid)

  asm-unit(is-toplevel) = asm-unit(is-toplevel|<newname> "-anon-")

  asm-unit(is-toplevel|name) = ?Unit-ASM(<id>)
  ; with(
      map(asm-command(is-toplevel))
    ; try(wrap-gzb-callee(|name))
    ; intersperse-concat(|"\n") => content
    ; rules(EmitFunction:+ _ -> (name, content))
    )
  ; <concat-strings> [<BaseNSID>, "/", name]

  wrap-gzb-callee(|""): commands* -> [pre*, commands*, post*]
    with
      pre* := [
        // push stack frame. storage container and path don't need to exist yet, will be created implicitly
        "data modify storage gzb:_internal _stack append value {}"
      ]
    ; post* := [
        // pop stack frame
        "data remove storage gzb:_internal _stack[-1]"
      ]

rules // asm-command(is-toplevel) :: Command-ASM -> string

  asm-command(is-toplevel): Command-Plain-ASM(cmd, args) -> <concat-strings> [cmd, " ", res]
    with
      res := <map(asm-arg); intersperse-concat(|" ")> args

  asm-command(is-toplevel): Command-CallGzb-ASM(nsid, args) -> "say todo: do gzb call here"

  asm-command(is-toplevel): Command-AnonSubUnit-ASM(unit@Unit-ASM(commands)) -> res
    with
      if is-toplevel then
        // flatten the sub unit if still at top level
        res := <map(asm-command(is-toplevel)); intersperse-concat(|"\n")> commands
      else
        res := <intersperse-concat(|" ")> ["function", <asm-unit(true)> unit]
      end

  asm-command(is-toplevel): Command-NoOp-ASM() -> "#no op"

  asm-command(is-toplevel): str -> str
    where is-string

rules // asm-arg :: Arg-ASM -> string

  asm-arg: Arg-RedirectSelf-ASM(args) -> res
    with
      res := <map(asm-arg); intersperse-concat(|" ")> args

  asm-arg: Arg-Redirect-ASM(cmd) -> res
    with
      res := <asm-command(false)> cmd

  asm-arg: Arg-NSID-ASM(NSID(namespace, name)) -> <concat-strings> [namespace, ":" | name]

  asm-arg: Arg-RawString-ASM(str) -> str

  asm-arg: str -> str
    where is-string
