module editor

imports

  common
  desugar
  generate
  asm

rules

  editor-desugar-llmc-local-pp: input@(selected, _, ast, path, _) -> (filename, result)
    with
      filename := <guarantee-extension(|"desugared.llmc")> path
    ; result := <desugar-function; pp-llmc> ast

  editor-generate-mcfn-asm-local: input@(selected, _, ast, path, _) -> (filename, result)
    with
      filename := <guarantee-extension(|"mcfn-asm.aterm")> path
    ; result := <generate-mcfn-asm> ast

  editor-generate-mcfn-shared: input@(selected, _, ast, path, project-path) -> (filenames, results)
    with
      !ast; ?Function-LL(NSID-LL(namespace, name), _, _, _)
    ; function_path := <intersperse-concat(|"/")> [namespace | name]
    ; function_base := NSID(namespace, name)
    ; asm_tree := <generate-mcfn-asm> ast
    with
      (filenames, results) := <
        asm(|function_base)
      ; map((mk-filename(|function_path); mk-src-gen-path(|project-path), id))
      ; unzip
      > asm_tree

rules

  mk-filename(|base): "" -> base
  mk-filename(|base): str -> <concat-strings> [base, "/", str]
    where not(?"")

strategies

  mk-src-gen-path(|project-path) =
    get-src-gen(|project-path, "gzb-fin-interm", ".mcfunction")
