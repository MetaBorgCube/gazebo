module transfer

imports

  signatures/llmc/-
  signatures/-

  common
  loc

  // BUG: transitive from 'common', but included here because otherwise the eclipse editor complains
  str_common

/** Transfer a raw literal value to a Loc. */
rules // transfer-raw(|Loc) :: string -> Command-ASM

  transfer-raw(|<loc-is-data => loc>): raw_string ->
    Command-Plain-ASM("data", [
      Arg-RawString-ASM("modify"),
      loc_args_data*,
      Arg-RawString-ASM("set"),
      Arg-RawString-ASM("value"),
      Arg-RawString-ASM(raw_string)
    ])
    with
      loc_args_data* := <loc-args-data> loc

  transfer-raw(|loc@Scoreboard(_, _)): raw_string ->
    Command-Plain-ASM("scoreboard", [
      Arg-RawString-ASM("players"),
      Arg-RawString-ASM("set"),
      Arg-RawString-ASM("value"),
      Arg-RawString-ASM(raw_string)
    ])

  transfer-raw(|Discard()): _ -> Command-NoOp-ASM()

/** Transfer the value of the source Loc to the given destination Loc. */
rules // transfer-loc(|Loc) :: Loc -> Command-ASM

  transfer-loc(|<loc-is-data => loc_dest>): <loc-is-data => loc_src> ->
    Command-Plain-ASM("data", [
      Arg-RawString-ASM("modify"),
      loc_dest_args*,
      Arg-RawString-ASM("set"),
      Arg-RawString-ASM("from"),
      loc_src_args*
    ])
    with
      loc_dest_args* := <loc-args-data> loc_dest
    ; loc_src_args* := <loc-args-data> loc_src

  transfer-loc(|loc_dest@Scoreboard(_, _)): loc_src@Scoreboard(_, _) ->
    Command-Plain-ASM("scoreboard", [
      Arg-RawString-ASM("players"),
      Arg-RawString-ASM("operation"),
      loc_dest_args*,
      Arg-RawString-ASM("="),
      loc_src_args*
    ])
    with
      loc_dest_args* := <loc-args-scoreboard> loc_dest
    ; loc_src_args* := <loc-args-scoreboard> loc_src

  // TODO: cross-transfer (e.g. between scoreboards and data)

  transfer-loc(|Discard()): _ -> Command-NoOp-ASM()
