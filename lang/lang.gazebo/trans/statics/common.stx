module statics/common

imports

    signatures/gazebo-sig
    statics/types

signature

    name-resolution

        labels
            P // parent
            M // module boundary
            I // star import (use)
            A // alias-of (for selectors)
            B // base (interfaces)
            O // origin (interfaces)

    sorts

        PROP
        MEMB_KIND
        NSIDi

    constructors

        PROP_OVERRIDABLE : TYPE -> PROP
        PROP_FINAL       : TYPE -> PROP

        MEMB_MODVARFN : MEMB_KIND
        MEMB_TYPE     : MEMB_KIND
        MEMB_ENUMKEY  : MEMB_KIND

        NSIDiFull  : string * list(string) -> NSIDi
        NSIDiShort : list(string)          -> NSIDi
        NSIDiRel   : list(string)          -> NSIDi

        /** alternative to Start with includes module name. injected by pre-analyze */
        StartInject : NSIDi * list(Use) * list(Tli) -> Start

    relations

        mod : NSIDi -> TYPE
        currmod : -> (NSIDi * TYPE)
        memb : (string * NSIDi * MEMB_KIND) -> TYPE
        field : string -> TYPE
        registry : NSIDi -> scope
        registration : NSIDi -> scope
        regmeta : string -> scope
        bs : string -> TYPE
        alias : string -> scope
        prop : string -> PROP
        /** injected type. contains the expected type of an expression in this scope. should n */
        tinj : -> TYPE
        /** expected return type of the current function */
        ret : -> TYPE
        /** current SELECTOR inner scope (for @s) */
        sender_selector : -> scope

rules

    withTypeInject : scope * TYPE -> scope

    withTypeInject(s, T) = s_tinj :-
        new s_tinj, s_tinj -P-> s,
        !tinj[T] in s_tinj.

rules

    currentMod : scope -> (NSIDi * scope)

    // TODO: this could maybe also be implemented without the explicit presence of the currmod relation
    currentMod(s) = (name, s_mod) :- {p p' T}
        query currmod
            filter P* and true
            min $ < P and false
            in s |-> p,
        p == [(p', (name, T))] | error $[no single mod, instead: [p]],
        T == MOD(s_mod).

rules

    stringListContains : list(string) * string
    stringListContains([], _) :- false.
    stringListContains([x], s) :- x == s.
    stringListContains([x|xs], s) :- x == s.

    stringListConcat : list(string) * list(string) -> list(string)
    stringListConcat([], r) = r.
    stringListConcat([x|xs], r) = [x | stringListConcat(xs, r)].

    stringListInit : list(string) -> list(string)
    stringListInit([]) = [].
    stringListInit([x]) = [].
    stringListInit([x | xs@[y|ys]]) = [x | stringListInit(xs)].
    stringListLast : list(string) -> string
    stringListLast([]) = "?" :- false | error $[cannot get last element from empty list].
    stringListLast([x]) = x.
    stringListLast([x|xs]) = stringListLast(xs).

    stringListFlatten : list(list(string)) -> list(string)
    stringListFlatten([]) = [].
    stringListFlatten([x]) = x.
    stringListFlatten([x|xs]) = stringListConcat(x, stringListFlatten(xs)).

rules

    nsidNameStrings : NSIDName -> list(string)
    nsidNameStrings(NSIDName(parts)) = nsidNamePartStrings(parts).

    nsidNamePartStrings maps nsidNamePartString(list(*)) = list(*)
    nsidNamePartString : NSIDNamePart -> string
    nsidNamePartString(NSIDNamePart(name)) = name.

rules

    nsidi : NSID -> NSIDi
    nsidi(NSID(ns, names)) = NSIDiFull(ns, nsidNameStrings(names)).
    nsidi(NSIDShort(names)) = NSIDiShort(nsidNameStrings(names)).
    nsidi(NSIDRel(names)) = NSIDiRel(nsidNameStrings(names)).

    nsidiName : NSIDi -> list(string)
    nsidiName(NSIDiFull(_, name)) = name.
    nsidiName(NSIDiShort(name)) = name.
    nsidiName(NSIDiRel(name)) = name.

rules

    /** rel * base -> abs */
    absolute : NSIDi * NSIDi -> NSIDi

    absolute(rel, base) = absoluteStrict(rel, base, (base_ns, base_name)) :-
        base == NSIDiFull(base_ns, base_name) | error $["absolute" called with non-full base].

rules

    /** rel * base * (rel_fallback_ns * rel_fallback_name) -> abs */
    absoluteStrict : NSIDi * NSIDi * (string * list(string)) -> NSIDi

    /** already absolute */
    absoluteStrict(rel@NSIDiFull(_, _), _, _) = rel.

    /** relative to namespace of base */
    absoluteStrict(NSIDiShort(rel), NSIDiFull(base_ns, _), _) = NSIDiFull(base_ns, rel).

    /** sub-name relative to base */
    absoluteStrict(rel@NSIDiRel(name), _, (rel_fallback_ns, rel_fallback_name)) = NSIDiFull(rel_fallback_ns, stringListConcat(stringListInit(rel_fallback_name), name)).

rules

    /** base * name -> concatenated */
    nsidiAppendName : NSIDi * list(string) -> NSIDi

    nsidiAppendName(NSIDiFull(ns, base_name), name) = NSIDiFull(ns, stringListConcat(base_name, name)).

    nsidiAppendName(NSIDiShort(base_name), name) = NSIDiShort(stringListConcat(base_name, name)).

    nsidiAppendName(NSIDiRel(base_name), name) = NSIDiRel(stringListConcat(base_name, name)).

rules

    absoluteInCurrMod : scope * string -> NSIDi

    absoluteInCurrMod(s, name) = NSIDiFull(ns, stringListConcat(names, [name])) :-
        currentMod(s) == (NSIDiFull(ns, names), _).

rules

    storeTliFullName : scope * ID

    storeTliFullName(s, name) :- {full}
        full == absoluteInCurrMod(s, name),
        @name.full := full.
