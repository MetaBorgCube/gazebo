module statics/use

imports

    statics/common
    statics/bind/mod
    statics/bind/memb

rules

    usesOk maps useOk(*, list(*))
    useOk : scope * Use

    useOk(s, Use(nsid)) :- {name nameLast}
        name == nsidiName(nsidi(nsid)),
        nameLast == stringListLast(name),
        useOk(s, UseAs(nsid, nameLast)).

    useOk(s, UseAs(nsid, as)) :- {T s_mod}
        resolveModRel(s, nsid) == T@MOD(s_mod) | error $[not a valid module: [T]]@nsid,
        s != s_mod | error $[can't import self]@nsid,
        // TODO: might want to add some metadata to the member declaration that it has been imported
        // TODO: ensure this is not resolved via an I-edge (e.g. by declaring it private)
        declareMembFromOrigin(s, s_mod, as, MEMB_MODVARFN(), T).

    useOk(s, UseFrom(nsid, froms)) :- {T s_mod}
        resolveModRel(s, nsid) == T@MOD(s_mod) | error $[not a valid module: [T]]@nsid,
        s != s_mod | error $[can't import self]@nsid,
        useFromsOk(s, s_mod, froms).

    useOk(s, UseFromStar(nsid)) :- {T s_mod}
        resolveModRel(s, nsid) == T@MOD(s_mod) | error $[not a valid module: [T]]@nsid,
        s != s_mod | error $[can't import self]@nsid,
        // add an import edge to make all (visible) members visible to the current scope
        s -I-> s_mod.

rules

    useFromsOk maps useFromOk(*, *, list(*))
    useFromOk : scope * scope * UseFromPart

    useFromOk(s, s_mod, UseFromPartRegular(id)) :-
        useFromOk(s, s_mod, UseFromPartAlias(id, id)).

    useFromOk(s, s_mod, UseFromPartAlias(id, as)) :- {T}
        resolveMembLocal(s_mod, id, MEMB_MODVARFN()) == (_, _, T) | error $[member [id] not found in module]@id,
        // TODO: might want to add some metadata to the member declaration that it has been imported
        // TODO: ensure this is not resolved via an I-edge (e.g. by declaring it private)
        declareMembFromOrigin(s, s_mod, as, MEMB_MODVARFN(), T),
        // may produce a warning if as==id (from above overload), because a type is already associated to id
        try { @as.type := T }.
