module statics/expr/tag-resource

imports

    statics/common
    statics/bind/registry
    statics/bind/registration
    statics/bind/regmeta
    statics/bind/bs
    statics/bind/tinj
    statics/expr
    statics/expr/nbt
    statics/type/bounds
    statics/type/utility

rules // tag

    typeOfTag : scope * Tag -> TYPE

    typeOfTag(s, Tag(_)) = TAG().

rules // resource

    typeOfResource : scope * Resource -> TYPE

    typeOfResource(s, ResourceBare(ident)) =
        typeOfResource(s, ResourceFull(ident, Props([]), NBTCompound([]))).

    typeOfResource(s, ResourceProps(ident, props)) =
        typeOfResource(s, ResourceFull(ident, props, NBTCompound([]))).

    typeOfResource(s, ResourceNBT(ident, payl)) =
        typeOfResource(s, ResourceFull(ident, Props([]), payl)).

    typeOfResource(s, ResourceFull(ident, Props(props), payl)) = T :- {id_registry s_registration}
        inferRegistry(s, ident) == ((id_registry, _), (_, s_registration)),
        T == RESOURCE(id_registry),
        resourcePropsOk(s, s_registration, props),
        resourcePaylOk(s, s_registration, payl).

rules // resource ident

    resourceIdent : Resource -> TRIdent
    resourceIdent(ResourceBare(ident)) = ident.
    resourceIdent(ResourceProps(ident, _)) = ident.
    resourceIdent(ResourceNBT(ident, _)) = ident.
    resourceIdent(ResourceFull(ident, _, _)) = ident.

rules // resource literal data

    /** get the inner data type of this resource. always COMPOUND */
    typeOfResourceData : scope * Resource -> TYPE

    typeOfResourceData(s, res) = COMPOUND(s_data) :- {ident refs s_registration}
        ident == resourceIdent(res),
        inferRegistry(s, ident) == refs@(_, (_, s_registration)),
        setRegistryReferences(ident, refs),
        s_data == resolveRegmeta(s_registration, "data").

rules

    /** infer registry and registration information from a resource identifier.
        function is publicly callable. */
    inferRegistry : scope * TRIdent -> ((NSIDi * scope) * (NSIDi * scope))

    inferRegistry(s, TRIdentExplicit(id_registry_raw, id_registration_raw)) = ((id_registry_full, s_registry), (id_registration_full, s_registration)) :-
        {id_registry} id_registry == nsidi(id_registry_raw),
        {id_registration} id_registration == nsidi(id_registration_raw),
        resolveRegistryRel(s, id_registry) == (id_registry_full, s_registry)
        | error $[registry not found]@id_registry_raw,
        resolveRegistration(s_registry, id_registration) == (id_registration_full, s_registration)
        | error $[registration not found in registry]@id_registration_raw.

    inferRegistry(s, TRIdentAnonymous(id_registration)) =
        inferRegistryAnon(s, resolveTinj(s, UNK()), nsidi(id_registration)).

rules

    /** attach AST properties to a resource identifer, given some registry+registration information.
        may only be called once per resource identifier, so it is intended to only be done via typeOfResource. */
    setRegistryReferences : TRIdent * ((NSIDi * scope) * (NSIDi * scope))

    setRegistryReferences(ident@TRIdentExplicit(id_registry_raw, id_registration_raw), ((id_registry_full, s_registry), (id_registration_full, s_registration))) :-
        @id_registry_raw.ref := id_registry_full,
        @id_registration_raw.ref := id_registration_full,
        @ident.registry_full := id_registry_full,
        @ident.registration_full := id_registration_full.

    setRegistryReferences(ident@TRIdentAnonymous(id_registration), ((id_registry_full, s_registry), (id_registration_full, s_registration))) :-
        @id_registration.ref := id_registration_full,
        @ident.registry_full := id_registry_full,
        @ident.registration_full := id_registration_full.

rules

    inferRegistryAnon : scope * TYPE * NSIDi -> ((NSIDi * scope) * (NSIDi * scope))

    inferRegistryAnon(s, Thint@RESOURCE(id_registry_hint), id_registration) = ((id_registry_full, s_registry), (id_registration_full, s_registration)) :-
        // no error checking on registry resolve, because type is provided as hint, so can be assumed to be valid
        resolveRegistryRel(s, id_registry_hint) == (id_registry_full, s_registry),
        resolveRegistration(s_registry, id_registration) == (id_registration_full, s_registration)
        | error $[registration not found in registry]@id_registration.

    inferRegistryAnon(s, UNK(), id_registration) =
        res@resolveRegistryAndRegistrationFromAllRegistries(s, allRegistries(s), id_registration) :- {registration}
        res == ((_, _), (registration, _)).

    inferRegistryAnon(_, _, id_registration) = _ :-
        false | error $[not valid in the current context].

rules

    resourcePropsOk maps resourcePropOk(*, *, list(*))
    resourcePropOk : scope * scope * Prop
    resourcePropOk(s, s_reg, KvProp(k, v)) :- {s_ss k' Tbs Te}
        // TODO: disallow repeat keys
        s_ss == resolveRegmeta(s_reg, "states") | error $[resource does not define any states],
        (k', Tbs) == resolveState(s_ss, k) | error $[undefined state for this resource]@k,
        @k.ref := k',
        Te == typeOfExpr(withTypeInject(s, Tbs), v),
        subtype(Te, Tbs) | error $[value not assignable to state with value type [Tbs]]@v.
    resourcePropOk(_, _, prop) :-
        false | error $[property type not supported]@prop.

rules

    resourcePaylOk : scope * scope * NBTCompound
    resourcePaylOk(_, _, NBTCompound([])).
    resourcePaylOk(s, s_reg, nbt) :- {s_data Tdata Tnbt}
        s_data == resolveRegmeta(s_reg, "data") | error $[resource does not define any data type],
        Tnbt == typeOfNBTCompound(s, nbt, Tdata@COMPOUND(s_data)),
        subtype(Tnbt, partial(Tdata)) | error $[payload not compatible with the defined data schema for this resource]@nbt.
