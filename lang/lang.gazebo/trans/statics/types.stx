module statics/types

imports

    signatures/gazebo/NSID-sig
    signatures/gazebo/Type-sig
    statics/common
    statics/bind/field
    statics/bind/memb
    statics/bind/registry
    statics/type/bounds
    statics/type/enum
    statics/type/interface

signature

    sorts TYPE

    constructors

        // Primitives
        STRING : TYPE
        BOOL : TYPE
        LONG : TYPE
        INT : TYPE
        SHORT : TYPE
        BYTE : TYPE
        DOUBLE : TYPE
        FLOAT : TYPE
        ENUM : scope * TYPE -> TYPE

        // Basic constructs
        POS : POS_KIND * TYPE -> TYPE
        RANGE : TYPE -> TYPE

        // Specific types
        SELECTOR : scope -> TYPE
        TAG : TYPE
        /** registry */
        RESOURCE : NSIDi -> TYPE
        ITER : TYPE -> TYPE
        VOID : TYPE

        // NBT-related exclusive types
        COMPOUND : scope -> TYPE
        LIST : TYPE -> TYPE
        ARRAY : TYPE -> TYPE

        // Modules
        MOD : scope -> TYPE

        // Function & Lambda
        FUNC : list((ID * TYPE)) * TYPE -> TYPE
        LAMBDA : TYPE

        // Misc
        /** univeral top type, anything is a subtype of UNK */
        UNK : TYPE
        /** universal bottom type, nothing is a subtype of BOTTOM */
        BOTTOM : TYPE
        ENUM_V_INT : int -> TYPE
        ENUM_V_STR : string -> TYPE
        OPTIONAL : TYPE -> TYPE

    sorts POS_KIND

    constructors

        /** absolute */
        POS_ABS : POS_KIND
        /** relative ("tilde notation") */
        POS_REL : POS_KIND
        /** local ("caret notation") */
        POS_LOC : POS_KIND
        /** any (only orignates from use-site) */
        POS_ANY : POS_KIND

rules

    typeOfType : scope * Type -> TYPE

    typeOfType(s, t) = T@typeOfTypeImpl(s, t) :-
        @t.type := T.

    /** do not reference directly, use typeOfType */
    typeOfTypeImpl : scope * Type -> TYPE

    typeOfTypeImpl(_, TypeString()) = STRING().
    typeOfTypeImpl(_, TypeBool()) = BOOL().
    typeOfTypeImpl(_, TypeInt()) = INT().
    typeOfTypeImpl(_, TypeFloat()) = FLOAT().

    typeOfTypeImpl(s, TypePos(PosAbs(), t)) = POS(POS_ABS(), typeOfType(s, t)).
    typeOfTypeImpl(s, TypePos(PosRel(), t)) = POS(POS_REL(), typeOfType(s, t)).
    typeOfTypeImpl(s, TypePos(PosLoc(), t)) = POS(POS_LOC(), typeOfType(s, t)).
    typeOfTypeImpl(s, TypePos(PosAny(), t)) = POS(POS_ANY(), typeOfType(s, t)).

    typeOfTypeImpl(_, TypeTag()) = TAG().

    // TODO: distinguish between different array/list fixed types

    typeOfTypeImpl(s, TypeRef(nsid)) = T :-
        T == resolveMembRef(s, nsid, MEMB_TYPE()).

    typeOfTypeImpl(s, TypeResourceRegistry(id_registry)) = RESOURCE(id_registry_full) :-
        resolveRegistryRel(s, nsidi(id_registry)) == (id_registry_full, _)
        | error $[registry not found]@id_registry,
        @id_registry.ref := id_registry_full.

    typeOfTypeImpl(s, TypeSelector()) = SELECTOR(s_sel_t) :-
        new s_sel_t.

    typeOfTypeImpl(s, TypeArrayDynamic(t)) = ARRAY(T) :-
        T == typeOfType(s, t).
    //typeOfTypeImpl(s, TypeArrayFixedAnonymous(t, size)) = ARRAY(T) :-
    //    T == typeOfType(s, t).
    //typeOfTypeImpl(s, TypeArrayFixedNamed(t, names)) = ARRAY(T) :-
    //    T == typeOfType(s, t).

    typeOfTypeImpl(s, TypeListDynamic(t)) = LIST(T) :-
        T == typeOfType(s, t).
    //typeOfTypeImpl(s, TypeListFixedAnonymous(t, size)) = LIST(T) :-
    //    T == typeOfType(s, t).
    //typeOfTypeImpl(s, TypeListFixedNamed(t, names)) = LIST(T) :-
    //    T == typeOfType(s, t).

    typeOfTypeImpl(s, T@TypeEnumUntyped(_)) =
        typeOfEnum(s, T).
    typeOfTypeImpl(s, T@TypeEnumTyped(_, _)) =
        typeOfEnum(s, T).

    typeOfTypeImpl(s, TypeInterface(membs)) =
        typeOfInterface(s, membs, []).
    typeOfTypeImpl(s, TypeInterfaceCompose(bases, membs)) =
        typeOfInterface(s, membs, bases).

    typeOfTypeImpl(s, TypeBottom()) = BOTTOM().
    typeOfTypeImpl(s, TypeVoid()) = VOID().

    typeOfTypeImpl(_, _) = UNK() :-
        try { false } | note $[todo: other types].

rules

    typeBoolish : TYPE

    typeBoolish(BOOL()).
    typeBoolish(SELECTOR(_)).
    typeBoolish(POS(_, INT())).
    typeBoolish(POS(_, FLOAT())).
    typeBoolish(_) :- false.

rules

    typeIntegral : TYPE

    typeIntegral(LONG()).
    typeIntegral(INT()).
    typeIntegral(SHORT()).
    typeIntegral(BYTE()).

rules

    typeNumeric : TYPE

    typeNumeric(LONG()).
    typeNumeric(INT()).
    typeNumeric(SHORT()).
    typeNumeric(BYTE()).
    typeNumeric(DOUBLE()).
    typeNumeric(FLOAT()).

rules

    typeListOrArray : TYPE
    typeListOrArray(LIST(_)).
    typeListOrArray(ARRAY(_)).

rules

    typeCountable : TYPE
    typeCountable(SELECTOR(_)).
    typeCountable(LIST(_)).
    typeCountable(ARRAY(_)).

rules

    typeComparable : Compare * TYPE * TYPE

    typeComparable(CmpGt(), T, T) :-
        typeNumeric(T).

    typeComparable(CmpGte(), T, T) :-
        typeNumeric(T).

    typeComparable(CmpLt(), T, T) :-
        typeNumeric(T).

    typeComparable(CmpLte(), T, T) :-
        typeNumeric(T).

    typeComparable(CmpEqual(), Tl, Tr) :-
        typeComparableEqual(Tl, Tr).

    typeComparable(CmpNotEqual(), Tl, Tr) :-
        // not equal is just a boolean inversion,
        // so same rules hold as for being CmpEqual()-comparable
        typeComparableEqual(Tl, Tr).

    typeComparable(CmpEquiv(), Tl, Tr) :-
        false | error $[reserved operator for future use].

    typeComparable(CmpIs(), SELECTOR(_), STRING()).
    typeComparable(CmpIs(), POS(_, Tpos), RESOURCE(NSIDiFull("minecraft", ["block"]))) :-
        typeNumeric(Tpos).

    typeComparable(CmpMatches(), T, RANGE(T)).

rules

    typeComparableEqual : TYPE * TYPE
    typeComparableEqual(STRING(), STRING()).
    typeComparableEqual(BOOL(), BOOL()).
    typeComparableEqual(LONG(), LONG()).
    typeComparableEqual(INT(), INT()).
    typeComparableEqual(SHORT(), SHORT()).
    typeComparableEqual(BYTE(), BYTE()).
    typeComparableEqual(DOUBLE(), DOUBLE()).
    typeComparableEqual(FLOAT(), FLOAT()).
    typeComparableEqual(ENUM(s_en, Tl), ENUM(s_en, Tr)) :-
        typeComparableEqual(Tl, Tr).
    typeComparableEqual(POS(_, Tl), POS(_, Tr)) :-
        typeComparableEqual(Tl, Tr).
    typeComparableEqual(RANGE(Tl), RANGE(Tr)) :-
        typeComparableEqual(Tl, Tr).
    typeComparableEqual(RESOURCE(reg), RESOURCE(reg)).
    typeComparableEqual(COMPOUND(_), COMPOUND(_)).
    typeComparableEqual(LIST(Tl), LIST(Tr)) :-
        typeComparableEqual(Tl, Tr).
    typeComparableEqual(ARRAY(Tl), ARRAY(Tr)) :-
        typeComparableEqual(Tl, Tr).

rules

    /** resolves to UNK if type is not iterable */
    typeUnwrapIter : TYPE -> TYPE
    typeUnwrapIter(ITER(T)) = T.
    typeUnwrapIter(LIST(T)) = T.
    typeUnwrapIter(ARRAY(T)) = T.
    typeUnwrapIter(RANGE(T)) = T.
    typeUnwrapIter(T) = UNK().

rules

    destructType : TYPE -> list(TYPE)
    destructType(POS(POS_ABS(), T)) = [T, T, T].
    destructType(t) = [] :- false | error $[cannot destruct type [t]].
