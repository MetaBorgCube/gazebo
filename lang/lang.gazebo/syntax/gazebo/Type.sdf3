module gazebo/Type

imports

    gazebo/Common
    gazebo/NSID
    gazebo/Expr

context-free sorts

    Type
    PosKind
    EnumTypedMemb
    InterfaceMember

context-free syntax

    Type.TypeRef = <<NSID>>

    Type.TypeString = <String>
    Type.TypeBool = <Bool>
    Type.TypeInt = <Int>
    Type.TypeFloat = <Float>

    Type.TypePos = <`<PosKind><Type>>
    PosKind.PosAbs = <>
    PosKind.PosRel = <~>
    PosKind.PosLoc = <^>
    PosKind.PosAny = <*>

    Type.TypeResourceRegistry = [$<[NSID]>]
    Type.TypeTag = [#]

    // TODO: support selector constraints
    Type.TypeSelector = [@]

    Type.TypeArrayDynamic = <[<Type>;]>
    Type.TypeArrayFixedAnonymous = <[<Type>; <INT>]>
    Type.TypeArrayFixedNamed = <[<Type>; <{ID ", "}+>]>
    Type.TypeListDynamic = <[<Type>]>
    Type.TypeListFixedAnonymous = <[<Type>: <INT>]>
    Type.TypeListFixedNamed = <[<Type>: <{ID ", "}+>]>

    Type.TypeEnumUntyped = <
        enum
        {
            <{ID ",\n"}*>
        }
    >
    Type.TypeEnumTyped = <
        enum <Type>
        {
            <{EnumTypedMemb ",\n"}*>
        }
    >

    Type.TypeInterface = <
        interface
        {
            <{InterfaceMember "\n"}*>
        }
    >
    Type.TypeInterfaceCompose = <
        interface : <{Type ", "}+>
        {
            <{InterfaceMember "\n"}*>
        }
    >

    EnumTypedMemb.EnumTypedMemb = <<ID> = <Expr>>

    InterfaceMember.InterfaceMember = <<ID> <Type>>
    InterfaceMember.InterfaceMemberString = <<STRING> <Type>>

lexical syntax

    ID = "String" {reject}
    ID = "Bool" {reject}
    ID = "Int" {reject}
    ID = "Float" {reject}
    ID = "enum" {reject}

context-free syntax // additional types for internal usage

    Type.TypeBottom = <__BOTTOM>
    Type.TypeVoid = <__VOID>

context-free sorts

    TypeDecl

context-free syntax

    TypeDecl.TypeDecl = <type <ID> <Type>>
