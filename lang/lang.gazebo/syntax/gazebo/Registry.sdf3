module gazebo/Registry

imports

    gazebo/Common
    gazebo/NSID
    gazebo/Expr
    gazebo/Type
    gazebo/Literals

context-free sorts

    Registry
    RegistryName
    RegistryMeta
    RegistryMetaEntry
    RmeBlockState

context-free syntax // common

    RegistryName.RegistryName = [$<[NSID]>]

context-free syntax // registry decl

    Registry.DynamicRegistry = <dynamic registry <RegistryName>>
    Registry.StaticRegistry = <
        static registry <RegistryName>
        {
            <RegistryMeta>
        }
    >

context-free syntax // meta

    RegistryMeta.RegistryMeta = <<{RegistryMetaEntry "\n"}+>>
    RegistryMetaEntry.RmeRegistration = <
        register <NSID>
        {
            <RegistryMeta>
        }
    >
    RegistryMetaEntry.RmeProtocolId = <protocol_id <INT>>
    RegistryMetaEntry.RmeBlockStates = <
        block_states
        {
            <{RmeBlockState "\n"}*>
        }
    >
    RegistryMetaEntry.RmeData = <data <Type>>

context-free syntax // meta specifics

    RmeBlockState.RmeBlockState = <<ID_UNRESTR> <Type> { <{Literal ", "}+> }>

template options

    keyword -/- [A-Za-z0-9]
